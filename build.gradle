buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'io.ratpack:ratpack-gradle:1.7.5'
        classpath 'org.postgresql:postgresql:9.4-1206-jdbc42'
//        classpath 'org.jooq:jooq-codegen:3.7.1'
        classpath 'org.jooq:jooq-codegen:3.12.3'
    }
}

apply plugin: "io.ratpack.ratpack-groovy"

repositories {
    jcenter()
}

run {
    jvmArgs "-Dratpack.port=5051"
}

dependencies {
    implementation 'org.postgresql:postgresql:9.4-1205-jdbc42'
//    implementation 'redis.clients:jedis:2.9.0'
//    implementation 'org.jooq:jooq:3.7.1'
    implementation 'org.jooq:jooq:3.12.3'
    implementation 'org.pac4j:pac4j-http:1.8.5'
    implementation 'org.pac4j:pac4j-oauth:1.8.5'
//    implementation 'joda-time:joda-time:2.9.4'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2'
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.25'
    implementation 'org.mindrot:jbcrypt:0.4'

    implementation ratpack.dependency('hikari')
    implementation ratpack.dependency('pac4j')

//    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
//        exclude module: 'groovy-all'
//    }
//    testImplementation ratpack.dependency('groovy-test')
}

task stage {
    dependsOn installDist
}

import org.jooq.util.jaxb.*
import org.jooq.util.*
import org.jooq.codegen.*

task jooqCodegen {
    doLast {
        Properties props = new Properties()
        props.load(new FileInputStream('src/ratpack/db.properties'))

        def writer = new StringWriter()
        def xml = new groovy.xml.MarkupBuilder(writer)
                .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.12.0.xsd') {
                    jdbc() {
                        driver('org.postgresql.Driver')
                        url("jdbc:postgresql://" +
                                "${props.get('postgres.serverName')}" +
                                ":" +
                                "${props.get('postgres.portNumber')}" +
                                "/" +
                                "${props.get('postgres.databaseName')}" +
                                "?ssl=true" +
                                "&sslmode=require" +
                                "&sslfactory=org.postgresql.ssl.NonValidatingFactory")
                        user(props.get('postgres.user'))
                        password(props.get('postgres.password'))
                    }
                    generator() {
                        database() {
                            includes('.*')
                            excludes('')
                            inputSchema('public')
                        }

                        // Watch out for this caveat when using MarkupBuilder with "reserved names"
                        // - https://github.com/jOOQ/jOOQ/issues/4797
                        // - http://stackoverflow.com/a/11389034/521799
                        // - https://groups.google.com/forum/#!topic/jooq-user/wi4S9rRxk4A
                        generate([:]) {
                            pojos true
                            daos true
                            pojosEqualsAndHashCode true
                            fluentSetters true
                        }

                        target() {
                            packageName('jooq.generated')
                            directory('src/main/java')
                        }
                    }
                }

        GenerationTool.generate(writer.toString())
    }
}
